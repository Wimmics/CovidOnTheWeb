construct {
  ?uri       rdfs:label ?uriLabel.
  ?uriParent rdfs:label ?uriParentLabel.
  ?uriClass  rdfs:label ?uriClassLabel.
  ?uriSubject  rdfs:label ?uriSubjectLabel.

  ?uri rdfs:subClassOf  ?uriParent;
       rdf:type         ?uriClass;
       dct:subject      ?uriSubject.
}
where {
    ?uri rdfs:label ?uriLabel. FILTER(lang(?uriLabel) in ("en", "es", "fr", "de"))

    { ?uri rdfs:subClassOf+ ?uriParent.
      ?uriParent rdfs:label ?uriParentLabel. FILTER(lang(?uriParentLabel) in ("en", "es", "fr", "de"))
    }
    UNION
    { ?uri rdf:type/rdfs:subClassOf* ?uriClass.
      ?uriClass rdfs:label ?uriClassLabel. FILTER(lang(?uriClassLabel) in ("en", "es", "fr", "de"))
    }
    UNION
    { ?uri <http://purl.org/linguistics/gold/hypernym> ?uriClass.
      ?uriClass rdfs:label ?uriClassLabel. FILTER(lang(?uriClassLabel) in ("en", "es", "fr", "de"))
    }
    UNION
    { ?uri dct:subject ?uriSubject.
      ?uriSubject rdfs:label ?uriSubjectLabel. FILTER(lang(?uriSubjectLabel) in ("en", "es", "fr", "de"))
    }
    UNION
    # Relationship of a taxon to its parent is mapped to subClassOf relationship
    { ?uri dbo:genus|dbo:family|dbo:order|dbo:phylum|dbo:kingdom|dbo:class ?uriParent.
      ?uriParent rdfs:label ?uriParentLabel. FILTER(lang(?uriParentLabel) in ("en", "es", "fr", "de"))
    }

    FILTER (?uri in (
        {{uri_list}}
    ))
}
